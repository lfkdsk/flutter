// Copyright 2014 The Flutter Authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

// This file is included from `<app>/android/settings.gradle`,
// so it can be versioned with the Flutter SDK.

import groovy.json.JsonSlurper

def flutterProjectRoot = rootProject.projectDir.parentFile

// Note: if this logic is changed, also change the logic in module_plugin_loader.gradle.
def pluginsFile = new File(flutterProjectRoot, '.flutter-plugins-dependencies')
if (!pluginsFile.exists()) {
  return
}

def object = new JsonSlurper().parseText(pluginsFile.text)
assert object instanceof Map
assert object.plugins instanceof Map
assert object.plugins.android instanceof List
// Includes the Flutter plugins that support the Android platform.
object.plugins.android.each { androidPlugin ->
  assert androidPlugin.name instanceof String
  assert androidPlugin.path instanceof String
  def pluginDirectory = new File(androidPlugin.path, 'android')
  assert pluginDirectory.exists()
  include ":${androidPlugin.name}"
  project(":${androidPlugin.name}").projectDir = pluginDirectory

  // BD ADD: START
  Properties gradleProperties = new Properties()
  File gradlePropertiesFile = flutterProjectRoot.toPath().resolve(androidPlugin.path).resolve('android').resolve("gradle.properties").toFile()
  if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withReader('UTF-8') { reader -> gradleProperties.load(reader) }
  } else {
    println("$name has no gradle.properties")
  }
  def extraModules = gradleProperties.getProperty("extra_module_include")
  if (extraModules != null) {
    def moduleIncludes = extraModules.split(",")
    moduleIncludes.each {
      println "gradle include $it"
      include ":$it"
      project(":$it").projectDir = flutterProjectRoot.toPath().resolve(androidPlugin.path).resolve('android').resolve("$it").toFile()
    }
  }
  // END
}
