def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

// The Gallery app's version is defined by the Flutter framework's version.
def flutterVersion = file("$flutterRoot/version").text
def flutterVersionComponents = flutterVersion.split(/[^0-9]+/)
// Let the integer version code of xx.yy.zz-pre.nnn be xxyyzznnn.
int flutterVersionNumber = Math.min(flutterVersionComponents[0].toInteger(), 20) * 100000000
flutterVersionNumber += Math.min(flutterVersionComponents[1].toInteger(), 99) * 1000000
flutterVersionNumber += Math.min(flutterVersionComponents[2].toInteger(), 999) * 1000
if (flutterVersionComponents.length > 3) {
    flutterVersionNumber += Math.min(flutterVersionComponents[3].toInteger(), 999)
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = "$flutterVersionNumber"
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = "$flutterVersion"
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.bytedance.sdk.bridge-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "io.flutter.demo.gallery"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
//            abiFilters "armeabi-v7a", "arm64-v8a"
            abiFilters "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    aaptOptions {
        // TODO(goderbauer): remove when https://github.com/flutter/flutter/issues/8986 is resolved.
        if(System.getenv("FLUTTER_CI_WIN")) {
            println "AAPT cruncher disabled when running on Win CI."
            cruncherEnabled false
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "com.bytedance.components:image:1.0.171.0.0"
    implementation 'com.bytedance.flutter:dynamicart:1.0.5'
    implementation 'com.bytedance.flutter:dynamicart-homepage:1.0.1'
    implementation 'com.bytedance.frameworks.baselib:utility:1.3.9'
    implementation 'com.ss.android.download:download-impl:1.1.9.1.3'
    implementation 'com.ss.ttm:ttvideoengine:1.9.6.14'
    implementation 'com.ss.ttm:player:2.9.4.71'
    implementation 'com.ixigua.common:videocore:1.0.22'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.bytedance.frameworks.core:commonmonitor:5.0.6.0-rc.4'
    implementation 'com.bytedance.frameworks.core:npth:3.0.0-rc.4'
    implementation 'com.bytedance.common_business:alog:1.3.2.6'

    // 国内版需要依赖mira库和头条网络库和下载库
    implementation 'com.bytedance.ies.social.base:downloader:1.1.3.32.3-tt'
    implementation "com.bytedance.frameworks.baselib:ttnet:2.0.6.4"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'com.bytedance.sdk.bridge:bridge:1.12.7-alpha.3'
    implementation 'com.bytedance.sdk.bridge:flutterbridge:1.12.0.0-SNAPSHOT'
    kapt 'com.bytedance.sdk.bridge:bridge_compiler:1.12.7-alpha.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
